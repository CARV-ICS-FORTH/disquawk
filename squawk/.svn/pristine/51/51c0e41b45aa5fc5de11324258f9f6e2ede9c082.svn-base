__release = 1;

/* our program's entry point */
ENTRY(_start)

/* list of our memory sections */
MEMORY
{
 ram    : o = 0,         l = 256k
 ramt   : o = 0x3fffc,   l = 0
 lflash : o = 0x1000000, l = 1024k
 hflash : o = 0x1100000, l = 1024k
}

/* allocation of input sections to output sections */
SECTIONS
{
__reserved_ram = __release ? 0x40 : 0x0A000;

 /* there will only be a reset section if we are targeting "release" */
 .reset 0x1010000: {
  *(.reset)
  __indata_from = .;
 } > lflash
 
 /* code and constants */
 .text __reserved_ram : AT (__release ? __indata_from : __reserved_ram) {
  __indata_start = .;
  *(.text)
  . = ALIGN (4);
  *(.strings)
  . = ALIGN (4);
  *(.rodata)
  . = ALIGN (4);
  *(.rodata.*)
  . = ALIGN (4);
  *(.data)
  __indata_end = .;
 } > ram 

 /* uninitialized data */
 .bss :
 {
  . = ALIGN (4);
   __bss_start = . ; 
  *(.bss)
  *(COMMON)
  . = ALIGN (4);
   __bss_end = . ;
   PROVIDE (end = .); 
 } > ram

 /* application stack */
 .stack :
 {
   __ram_top = .;
   __heap_limit = . - 8192; /* stack allowance */
 } > ramt

}
