MEMORY {
	config      : ORIGIN = 0x1ffffff4, LENGTH = 0x0c
	ext_ram     : ORIGIN = 0x20000000, LENGTH = 512k
	ext_ram_top : ORIGIN = 0x20080000, LENGTH = 0x0
}

SECTIONS {
	.config : {
		LONG(bss_start);
		LONG(bss_end);
		LONG(arm_main);
	} > config

	.data : {
		*(.lowdata)
		*(.data)
		*(.glue_7*)
		. = ALIGN(4);
	} > ext_ram

	.text : {
		*(.text)
		*(.rodata)
		*(.rodata.*)
		. = ALIGN(4);
	} > ext_ram

	.bss : {
		bss_start = . ;
		*(.bss)
		*(COMMON)
		. = ALIGN(4);
		bss_end = . ;
		heap_start = . ;
	} > ext_ram

	.stack : {
		heap_end = . - 8192 - 16384; /* 8k for stacks, 16k for page table */
	} > ext_ram_top
}
